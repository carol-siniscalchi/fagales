#####################################
# GTR+G nucleotide substitution model #
#####################################

# Initialize the parameters of substitution matrix #
# Both the stationary frequencies and exchangeability rates #
# are drawn from uniform Dirichlet distributions #

# First the stationary frequencies #
sf_hp <- v(1,1,1,1)
sf ~ dnDirichlet(sf_hp)

# Then the exchangeability rates #
er_hp <- v(1,1,1,1,1,1)
er ~ dnDirichlet(er_hp)

moves.append( mvSimplexElementScale(er, alpha=10.0, weight=5.0) )
moves.append( mvSimplexElementScale(sf, alpha=10.0, weight=5.0) )

# Create the matrix #
Q_dna := fnGTR(er,sf)

# Create a discretized gamma distribution of rate categories  #
# the shape parameter is drawn from an exponential prior #
alpha_dna ~ dnExponential( 1.0 )

moves.append( mvScale(alpha_dna, lambda=0.01, weight=2.0) )
moves.append( mvScale(alpha_dna, lambda=0.1,  weight=2.0) )
moves.append( mvScale(alpha_dna, lambda=1,    weight=2.0) )

rates_dna := fnDiscretizeGamma( alpha_dna, alpha_dna, 4 )

### Create the substitution model and clamp with our observed DNA data ###
phySeq ~ dnPhyloCTMC(tree=fbd_tree, Q=Q_dna, siteRates=rates_dna, branchRates=branch_rates, type="DNA")
phySeq.clamp(dna)
